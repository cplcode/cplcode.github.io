USE rbmat
USE symbolic
! USE rtchecks
REAL parm[1..2]
A0==parm(1); B==parm(2) !(; A1==parm(3) !)

profile==utau*[B+A0*LOG(utau*z)+A1*g*z/h]

B=4.48; A0=2.55; A1=1; utau=1; g=2
REAL z,y,AA,BB(1..parm.HI),CC(1..parm.HI,1..parm.HI),Sigma(1..parm.HI,1..parm.HI)
INTEGER count
CHAR name(80)
REAL h
LOOP newt
AA=0; BB=0; CC=0; count=0
list=OPEN(COMMANDLINE(1))
LOOP WHILE READ FROM list name,h
READ FROM list
WRITE name,h
in=OPEN(name)
LOOP WHILE READ FROM in "#"; READ FROM in; REPEAT
LOOP WHILE READ FROM in z,y
 IF z>=200 AND z<=h/2 THEN
  INC count
  d==profile-y
  AA=~+d^2/2
  DO BB(i)=~+d*D(profile,parm(i)) FOR ALL i
  DO CC(i,j)=~+D[d*D[profile,parm(i)],parm(j)] FOR ALL i,j
 END IF 
REPEAT
CLOSE in
REPEAT
CLOSE list
Sigma=INV(CC)
dp=Sigma*BB
parm=~-dp
WRITE count,ABS(dp)
REPEAT newt UNTIL ABS(dp)<1E-5
DO WRITE parm(i),SQRT[Sigma(i,i)*AA/(count-parm.HI)] FOR i=1 TO HI
