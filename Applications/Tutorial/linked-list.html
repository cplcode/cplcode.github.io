<!DOCTYPE html><html lang="en"><head></head><body style="font-size:large"><pre>
! Linked-list version of the Gauss-Seidel update
! ==============================================

USE gnuplot

TYPE CELL=STRUCTURE(POINTER TO CELL next; INTEGER col; REAL var, rhs)
!   declare a new STRUCTURE type. "TYPE" is optional here

POINTER TO CELL RowStart(0..20) !   declare row pointers
LOOP FOR ALL i !   allocate and fill in the sparse-array structures
  POINTER TO POINTER TO CELL p=RowStart(i)
  LOOP FOR j=i TO i+20
    p^=NEW CELL !   here explicit dereferencing is needed for = assignment
    p.col=j; p.var=0; p.rhs=0
    p=p.next
  REPEAT LOOP
  p^=NULL !   here explicit dereferencing is needed for = assignment
REPEAT LOOP

DO p.var=1 FOR p=RowStart(HI) UNTIL p=NULL BY p=p.next
! assign some boundary condition

INLINE REAL FUNCTION val(POINTER TO POINTER TO CELL p; INTEGER c)
  LOOP WHILE p^#NULL POINTER TO CELL AND p.col&lt;c: p=p.next
  !   move pointer p until column c or end of row
  IF p.col=c THEN RESULT=p.var ELSE RESULT=0 !  return zero if element not found
END val !   otherwise return element's "var" value

LOOP FOR 100 TIMES   ! main iteration loop
  LOOP FOR i=LO+1 TO HI-1   ! for each row
    POINTER TO CELL above=RowStart(i+1),
    left=RowStart(i), center=left.next, right=center.next,
    below=RowStart(i-1)   ! initialize pointers to surrounding cells
    LOOP WHILE right#NULL
      j=center.col   ! read column index stored in central cell
      center.var=[val(above,j)+val(below,j)+val(left,j-1)+val(right,j+1)+
        center.rhs]/4
      ! with the "val" trick, notation is very similar to standard 
      center=center.next
      IF right=center THEN right=right.next
    REPEAT LOOP
  REPEAT LOOP
REPEAT LOOP

! now plot something
RANGE (0)..(40),(0)..(2)
LOOP FOR i=RowStart.LO TO RowStart.HI
  STARTLINE; DO DRAW p.col,p.var+i/20 FOR p=RowStart(i) UNTIL p=NULL BY p=p.next
REPEAT
SHOWGRAPH
READ


!(
  C-like (or Pascal-like) structures and pointers are a standard part of
  CPL, but dereferencing is implicit. Only three ^'s need appear in the above
  program, all beside = or # operators.
!)
</pre></body></html>
