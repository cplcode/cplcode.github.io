USE gnuplot
USE symbolic
! USE rtchecks
nx=40
dx=1/nx
m=10
REAL u(0..nx,0..nx)=0, px=1
INTEGER f(0..nx,0..nx)
DO f(ix,iy)=IF (ix/nx-0.5)^2+(iy/nx-0.5)^2<0.25 THEN 1 ELSE 0 FOR ALL ix,iy
DO u(i,j)=3*f(i,j) FOR i=5 TO nx DIV 2 AND j=5 TO nx DIV 2
mod=={[lu(i+1,j+1)-lu(i,j+1)]^2+[lu(i+1,j)-lu(i,j)]^2+[lu(i+1,j+1)-lu(i+1,j)]^2+[lu(i,j+1)-lu(i,j)]^2}/dx^2/2
mu==(EXP(-mod/m)+0.05)
J==mu*mod
LOOP iter FOR n=1 TO 10000000
  REAL maxrsd=0
  LOOP FOR parity=0 TO 1 AND ix=1 TO nx-1 AND iy=2-(ix+parity) MOD 2 TO nx-1 BY 2 EXCEPT f(ix,iy)=0
    lu=^u(ix+(-1..1),iy+(-1..1))
    rsd=SUM D(J,lu(0,0))-px FOR i=-1 TO 0 AND j=-1 TO 0
    mu0=SUM mu FOR i=-1 TO 0 AND j=-1 TO 0
    lu(0,0)=~-dx*dx/4/mu0*rsd
    maxrsd=MAX(maxrsd,ABS(rsd))
  REPEAT
  fr=dx^2*(SUM u(ix,iy) FOR ALL ix,iy)
  px=px*(0.8+0.2/fr)
IF n MOD 10 =0 THEN WRITE maxrsd; SPLOT u
REPEAT iter
